plugins {
    id 'org.asciidoctor.convert' version '1.5.6'
}

group = 'io.github.solomkinmv.glossary'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 10

apply plugin: 'com.palantir.docker'

dependencies {
    compile awsSdk

    compile('org.springframework.boot:spring-boot-starter-actuator')
	compile('org.springframework.cloud:spring-cloud-starter-config')
    compile('org.springframework.boot:spring-boot-configuration-processor')
    compile('org.springframework.cloud:spring-cloud-starter-netflix-eureka-client')
	compile('org.springframework.boot:spring-boot-starter-hateoas')
	compile('org.springframework.boot:spring-boot-starter-web')
	compileOnly lombok

    compile jaxbApi // should be included as external dependency because of java 9
    compile jaxbCore// should be included as external dependency because of java 9
    compile jaxbImpl// should be included as external dependency because of java 9
    compile activation// should be included as external dependency because of java 9

    // zipkin
    compile('org.springframework.cloud:spring-cloud-starter-sleuth')
    compile('org.springframework.cloud:spring-cloud-starter-zipkin')
    compile('org.springframework.amqp:spring-rabbit')

	testCompile('org.springframework.boot:spring-boot-starter-test')
	testCompile('org.springframework.restdocs:spring-restdocs-mockmvc')
}

docker {
    name "solomkinmv/${bootJar.baseName}"
    tags project.version
    println "==> Docker task for ${name}"
    files bootJar.archivePath
    buildArgs(['JAR_FILE': "${bootJar.archiveName}"])
}

ext {
	snippetsDir = file('build/generated-snippets')
}

test {
	outputs.dir snippetsDir
}

asciidoctor {
	attributes 'snippets': snippetsDir
	inputs.dir snippetsDir
	dependsOn test
}

bootJar {
    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
}