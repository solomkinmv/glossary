def jsonWebTokenVersion = '0.7.0'
def httpClientVersion = '4.5.2'
def awsSdkVersion = '1.11.181'
def jlayerVersion = '1.0.1'
def flickrVersion = '2.16'
def guavaVersion = '21.0'
def lombokVersion = '1.16.20'
def jaxbApiVersion = '2.3.0'
def thumbnailatorVersion = '0.4.8'

ext {
    springCloudVersion = 'Finchley.M5'

    awsSdk = "com.amazonaws:aws-java-sdk:${awsSdkVersion}"
    jsonwebtoken = "io.jsonwebtoken:jjwt:${jsonWebTokenVersion}"
    jlayer = "javazoom:jlayer:${jlayerVersion}"
    flickr4java = "com.flickr4java:flickr4java:${flickrVersion}"
    guava = "com.google.guava:guava:${guavaVersion}"
    httpClient = "org.apache.httpcomponents:httpclient:${httpClientVersion}"
    fluentHC = "org.apache.httpcomponents:fluent-hc:${httpClientVersion}"
    lombok = "org.projectlombok:lombok:${lombokVersion}"
    jaxbApi = "javax.xml.bind:jaxb-api:${jaxbApiVersion}"
    thumbnailator = "net.coobird:thumbnailator:${thumbnailatorVersion}"
}

buildscript {
    ext {
        springBootVersion = '2.0.0.M7'
        palantirDockerPluginVersion = '0.17.2'

    }

    repositories {
        mavenCentral()
        maven { url 'https://repo.spring.io/libs-snapshot' }
        maven { url 'https://repo.spring.io/libs-milestone' }
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath("gradle.plugin.com.palantir.gradle.docker:gradle-docker:${palantirDockerPluginVersion}")
    }
}

subprojects {
    // todo: remove this block after migration to microservices
    group 'io.github.solomkinmv'
    version '0.0.2-SNAPSHOT'

    repositories {
        mavenCentral()
        jcenter()
        maven { url 'https://repo.spring.io/libs-snapshot' }
        maven { url 'https://repo.spring.io/libs-milestone' }
    }

    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }
}